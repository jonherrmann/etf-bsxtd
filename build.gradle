///////////////////////////////////////////////////////////////////////////////////////
//
// ETF-BsxTD
//
///////////////////////////////////////////////////////////////////////////////////////

buildscript {
	repositories {
		maven {
			url "http://services.interactive-instruments.de/etfdev-af/plugins-releases-local"
			credentials {
				username 'ii-bda'
				password '6ozhS683'
			}}
	}
	dependencies {
		classpath group: 'de.interactive_instruments.bda', name: 'etf-bda', version:'1.0.23'
	}
	dependencies {
		ant.unjar src: configurations.classpath.files.find {it.path.contains('etf')}, dest: 'build/gradle'
	}
}
apply from: 'build/gradle/ii-bda.gradle'

///////////////////////////////////////////////////////////////////////////////////////

group = 'de.interactive_instruments.etf.testdriver'
description = "ETF BaseX test driver"


ext.testDataDir = project.hasProperty('etf.td.deployment.dir') ?
		project.getProperty('etf.td.deployment.dir') :
		project.file('./build/tmp/td')



repositories {
	mavenLocal()

	// BaseX
	maven { url 'http://files.basex.org/maven' }
}

ext.basexVersion = '8.5.3'

dependencies {

	compileOnly group: 'de.interactive_instruments', name: 'ii-commons-util', version:'2.0.0'+project.snapshotSuffix
	compileOnly group: 'de.interactive_instruments.etf', name: 'etf-core', version:'1.0.0'+project.snapshotSuffix
	compileOnly group: 'de.interactive_instruments.etf', name: 'etf-spi', version:'1.0.0'+project.snapshotSuffix

	compile group: 'commons-logging', name: 'commons-logging', version:'1.1.1'

	compile group: 'commons-io', name: 'commons-io', version: etf_commonsIoVersion

	// BaseX
	compileOnly group: 'org.basex', name: 'basex', version: basexVersion

	compileOnly group: 'net.sf.saxon', name: 'Saxon-HE', version: etf_saxonHe

	compileOnly group: 'org.slf4j', name: 'slf4j-api', version: etf_slf4jApiVersion



	// Required by BaseX extensions, must be placed in the lib folder or
	// the extension class loader will not initialize 3rd party libraries correctly
	compileOnly group: 'xerces', name: 'xercesImpl', version: etf_xercesVersion
	compileOnly group: 'xml-apis', name: 'xml-apis', version: etf_xmlApisVersion
	compileOnly('dom4j:dom4j:1.6.1') {
		exclude group: 'xml-apis'
	}

	testCompile group: 'junit', name: 'junit', version: etf_junitTestVersion
}
configurations {
	compile.exclude group:'xalan'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	description = 'Creates jars with the sourcecode'
	classifier 'source'
	from sourceSets.main.allSource
}

jar.doFirst {
	manifest {
		attributes(
				'Package-Title': 'BaseX',
				'Package-Vendor': 'BaseX GmbH'
		)
	}
}

task copyTdToTdDir(dependsOn: jar) << {
	copy {
		from jar
		into "$testDataDir/bsx/bin"
	}
	println "Copied test driver to $testDataDir"
}

def allLibs = configurations.runtime + configurations.compile -
		configurations.compile.filter { it.name.startsWith('etf-model') } -
		configurations.compile.filter { it.name.startsWith('ii-commons-util') } -
		configurations.compile.filter { it.name.startsWith('slf4j-api') } -
		configurations.compile.filter { it.name.startsWith('jetty') } -
		configurations.compile.filter { it.name.startsWith('xml-apis') } -
		configurations.compile.filter { it.name.startsWith('log4j') }

task copyLibsToTdDir << {
	copy {
		from allLibs
		into "$testDataDir/bsx/lib"
	}
}

install {
	dependsOn = ["driverZip", "copyTdToTdDir", "copyLibsToTdDir"]
}

task driverZip(type: Zip) {
	baseName = 'etf-bsxtd'
	into('bsx/bin') {
		from jar
	}
	into('bsx/lib') {
		from allLibs
	}
}

artifacts {
	archives driverZip
}
